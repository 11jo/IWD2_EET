IF
	Clicked([ANYONE])
	!Range(LastTrigger,4)
THEN
	RESPONSE #100
		SetGlobal("41PrismRotate","MYAREA",1)
		ActionOverride(LastTrigger,MoveToPoint([2055.1200]))
END

IF
	OR(2)
		Clicked([ANYONE])
		Global("41PrismRotate","MYAREA",1)
	Range(LastTrigger,4)
THEN
	RESPONSE #100
		SetGlobal("41PrismRotate","MYAREA",2)
		Continue()
END

IF
	Global("PrismDeactivated","MYAREA",1)
	Global("41PrismRotate","MYAREA",2)
	//CheckSkillGT(LastTrigger,6,Use_Magic_Device)
	CheckStatGT(LastTrigger,80,LORE)
THEN
	RESPONSE #100
		FadeToColor([0.0],0)
		SetGlobal("41PrismRotate","MYAREA",0)
		SetGlobal("PrismDeactivated","MYAREA",0)
		DisplayString(LastTrigger,@39936)
		AdvanceTime(300)
		FadeFromColor([0.0],0)
END

IF
	Global("PrismDeactivated","MYAREA",1)
	Global("41PrismRotate","MYAREA",2)
THEN
	RESPONSE #100
		SetGlobal("41PrismRotate","MYAREA",0)
		DisplayString(LastTrigger,@30529)
END

IF
	Global("41PrismRotate","MYAREA",2)
THEN
	RESPONSE #100
		PlaySound("IW4102a")
		Continue()
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",2)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_AWAKE)
		//DisplayMessage(32075)  // Теперь призма направлена на северо-восток.
		DisplayStringPointLog([2025.1180],@32075)
		SetGlobal("PrismSequence","MYAREA",1)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",3)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_CAST)
		//DisplayMessage(32076)  // Теперь призма направлена на восток.
		DisplayStringPointLog([2025.1180],@32076)
		SetGlobal("PrismSequence","MYAREA",2)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",4)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_CONJURE)
		//DisplayMessage(32077)  // Теперь призма направлена на юго-восток.
		DisplayStringPointLog([2025.1180],@32077)
		SetGlobal("PrismSequence","MYAREA",3)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",5)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_DAMAGE)
		//DisplayMessage(32078)  // Теперь призма направлена на юг.
		DisplayStringPointLog([2025.1180],@32078)
		SetGlobal("PrismSequence","MYAREA",4)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",6)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_DIE)
		//DisplayMessage(32082)  // Теперь призма направлена на юго-запад.
		DisplayStringPointLog([2025.1180],@32082)
		SetGlobal("PrismSequence","MYAREA",5)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",7)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_HEAD_TURN)
		//DisplayMessage(32083)  // Теперь призма направлена на запад.
		DisplayStringPointLog([2025.1180],@32083)
		SetGlobal("PrismSequence","MYAREA",6)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",0)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_READY)
		//DisplayMessage(32084)  // Теперь призма направлена на северо-запад.
		DisplayStringPointLog([2025.1180],@32084)
		SetGlobal("PrismSequence","MYAREA",7)
		SetGlobal("41PrismRotate","MYAREA",0)
END

IF
	Global("41PrismRotate","MYAREA",2)
	Global("PrismSequence","MYAREA",1)
THEN
	RESPONSE #100
		StaticSequence("Prism",SEQ_ATTACK)
		//DisplayMessage(32074)  // Теперь призма направлена на север.
		DisplayStringPointLog([2025.1180],@32074)
		SetGlobal("PrismSequence","MYAREA",0)
		SetGlobal("41PrismRotate","MYAREA",0)
END

