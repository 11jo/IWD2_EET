IF
	!Allegiance(Myself,ENEMY)
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	Global("BRASTON_PAUSED_STATE","GLOBAL",2)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("BRASTON_PAUSED_STATE","GLOBAL",0)
		Continue()
END

IF
	Global("BRASTON_KNOWS_ENELIA_DEAD","GLOBAL",0)
	See("Ennelia")
	Dead("Ennelia")
THEN
	RESPONSE #100
		SetGlobal("BRASTON_KNOWS_ENELIA_DEAD","GLOBAL",1)
		DisplayStringHead(Myself,@17051)
		Continue()
END

IF
	//HasItemInSlot("HQLSWRD",Myself,SLOT_WEAPON0)
	!HasItemSlot(Myself,SLOT_WEAPON0)
	HasItem("HQLSWRD",Myself)
THEN
	RESPONSE #100
		XEquipItem("HQLSWRD",Myself,SLOT_WEAPON0,EQUIP)
		EquipMostDamagingMelee()
END

IF
	Global("BRASTON_SAW_ENNELIA","MYAREA",0)
	See("Ennelia")
THEN
	RESPONSE #100
		SetGlobal("ENNELIA_BRASTON_TEAM","MYAREA",1)
		SetGlobalTimer("IwTimer4","LOCALS",5)
		SetGlobal("BRASTON_SAW_ENNELIA","MYAREA",1)
		DisplayStringHead(Myself,@17051)
END

IF
	Global("BRASTON_SAW_ENNELIA","MYAREA",0)
	GlobalGT("30BRASTO_ESCAPE","GLOBAL",3)
	!See("Ennelia")
	
	See([PC])
THEN
	RESPONSE #100
		SetGlobal("ENNELIA_BRASTON_TEAM","MYAREA",1)
		SetGlobalTimer("IwTimer4","LOCALS",5)
		FaceObject(LastSeenBy(Myself))
		SetGlobal("BRASTON_SAW_ENNELIA","MYAREA",1)
		DisplayStringHead(Myself,@32199)
END

IF
	Global("ENNELIA_BRASTON_TEAM","MYAREA",0)
	!Global("BRASTON_SAW_ENNELIA","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("ENNELIA_BRASTON_TEAM","MYAREA",1)
		Continue()
END

IF
	Global("SAW_ENEMY","LOCALS",0)
	!Allegiance(Myself,ENEMY)
	See([ENEMY])
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@32099)
		SetGlobal("SAW_ENEMY","LOCALS",1)
		Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

IF
	!HasWeaponEquiped(Myself)
	AttackedBy([ENEMY],DEFAULT)
THEN
	RESPONSE #100
		RunAwayFrom(LastAttackerOf(Myself),90)
END

IF
	!Global("SAW_ENEMY","LOCALS",0)
	ActionListEmpty()
	!HasWeaponEquiped(Myself)
	See([ENEMY])
THEN
	RESPONSE #100
		FaceObject(LastSeenBy(Myself))
END

IF
	HasWeaponEquiped(Myself)
	!Allegiance(Myself,ENEMY)
	See([ENEMY])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

IF
	HasWeaponEquiped(Myself)
	AttackedBy([ENEMY],DEFAULT)
THEN
	RESPONSE #100
		AttackOneRound(LastAttackerOf(Myself))
END

IF
	!Global("SAW_ENEMY","LOCALS",0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("SAW_ENEMY","LOCALS",0)
		Continue()
END

IF
	Global("BRASTON_SAW_ENNELIA","MYAREA",1)
	Global("SAW_ENEMY","LOCALS",0)
	!GlobalTimerNotExpired("IwTimer4","LOCALS")
	See("Ennelia")
THEN
	RESPONSE #100
		SetGlobal("BRASTON_SAW_ENNELIA","MYAREA",2)
		SetGlobalTimer("IwTimer4","LOCALS",5)
		DisplayStringHead(Myself,@17057)
END

IF
	!Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_REALLY_DEAD)
	Global("BRASTON_ENNELIA_LEAVE","GLOBAL",0)
	!GlobalTimerNotExpired("IwTimer4","LOCALS")
	OR(2)
		Global("BRASTON_SAW_ENNELIA","MYAREA",2)
		!Global("BRASTON_KNOWS_ENELIA_DEAD","GLOBAL",0)
	See([PC])
THEN
	RESPONSE #100
		//SetDialogueRange(300)
		//Dialogue(LastMarkedObject)
		ApplySpellRES("BDCUTSPY",Myself)
		StartDialogNoSet(LastSeenBy(Myself))
END

IF
	!Global("IWPERAI","LOCALS",1)
THEN
	RESPONSE #100
		ApplySpellRES("IWPERAI",Myself)
		SetGlobal("IWPERAI","LOCALS",1)
		Continue()
END

