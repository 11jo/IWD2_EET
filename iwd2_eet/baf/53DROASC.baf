IF
	!Global("SmokeBomb","LOCALS",0)
	GlobalTimerExpired("IwTimer50","LOCALS")
	//CreatureHidden(Myself)
	!Global("CreatureHidden","LOCALS",0)
THEN
	RESPONSE #100
		//SpellHitEffectSprite(Myself,Myself,CONE_OF_COLD,10)
		CreateVisualEffectObject("COCOLDH",Myself)
		SmallWait(5)
		//SpellHitEffectSprite(Myself,Myself,SOLS_SEARING_ORB,10)
		CreateVisualEffectObject("SSORBH",Myself)
		//HideCreature(Myself,FALSE)
		ApplySpellRES("IDINVIS7",Myself)
		SetGlobal("CreatureHidden","LOCALS",0)
		Continue()
END

IF
	!Global("SmokeBomb","LOCALS",0)
	!GlobalTimerExpired("IwTimer50","LOCALS")
	//CreatureHidden(Myself)
	!Global("CreatureHidden","LOCALS",0)
THEN
	RESPONSE #100
		RandomWalk()
END

IF
	HasItem("POTN52",Myself) //POTN_08
	HPPercentLT(Myself,33)
THEN
	RESPONSE #100
		UseItem("POTN52",Myself) //POTN_08
END

IF
	Global("SmokeBomb","LOCALS",0)
	//!CreatureHidden(Myself)
	Global("CreatureHidden","LOCALS",0)
	//!IsCreatureHiddenInShadows(Myself)
	!StateCheck(Myself,STATE_NOT_VISIBLE)
	HPPercentLT(Myself,75)
	Range(NearestEnemyOf(Myself),2)
THEN
	RESPONSE #100
		SetGlobal("SmokeBomb","LOCALS",1)
		//SpellHitEffectSprite(Myself,Myself,CONE_OF_COLD,10)
		CreateVisualEffectObject("COCOLDH",Myself)
		SmallWait(5)
		//SpellHitEffectSprite(Myself,Myself,SOLS_SEARING_ORB,10)
		CreateVisualEffectObject("SSORBH",Myself)
		//HideCreature(Myself,TRUE)
		ApplySpellRES("IDINVIS6",Myself)
		SetGlobal("CreatureHidden","LOCALS",1)
		ReallyForceSpellRES("IWWI313",Myself)  // SPWI313.SPL WIZARD_SKULL_TRAP
		SetGlobalTimer("IwTimer50","LOCALS",7)
		RandomWalk()
END

IF
	GlobalLT("SmokeBomb","LOCALS",2)
	//!CreatureHidden(Myself)
	Global("CreatureHidden","LOCALS",0)
	//!IsCreatureHiddenInShadows(Myself)
	!StateCheck(Myself,STATE_NOT_VISIBLE)
	HPPercentLT(Myself,50)
	Range(NearestEnemyOf(Myself),2)
THEN
	RESPONSE #100
		SetGlobal("SmokeBomb","LOCALS",2)
		//SpellHitEffectSprite(Myself,Myself,COLD,0)
		CreateVisualEffectObject("COLDH",Myself)
		//HideCreature(Myself,TRUE)
		ApplySpellRES("IDINVIS6",Myself)
		SetGlobal("CreatureHidden","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_GLYPH_OF_WARDING)  // SPPR304.SPL CLERIC_GLYPH_OF_WARDING
		SetGlobalTimer("IwTimer50","LOCALS",7)
		RandomWalk()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	//!StateCheck(Myself,STATE_INVISIBLE)
	//IsCreatureHiddenInShadows(Myself)
	StateCheck(Myself,STATE_NOT_VISIBLE)
	//!CreatureHidden(Myself)
	Global("CreatureHidden","LOCALS",0)
	OR(2)
		See(NearestEnemyOf(Myself))
		See(WeakestOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		//BackStab(LastSeenBy(Myself))
		AttackReevaluate(LastSeenBy(Myself),30)
END

IF
	//!CreatureHidden(Myself)
	Global("CreatureHidden","LOCALS",0)
	OR(2)
		See(NearestEnemyOf(Myself))
		See(WeakestOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END

