IF
	//!IsHeartOfFuryModeOn()
	!NightmareModeOn()
	HPPercentLT(Myself,40)
THEN
	RESPONSE #100
		MoraleSet(Myself,0)
		Panic()
END

IF
	Dead("DRUM_3")
	Dead("DRUM_2")
	Dead("DRUM_1")
THEN
	RESPONSE #100
		ChangeAIScript("",CLASS)
END

IF
	//!IsHeartOfFuryModeOn()
	!NightmareModeOn()
	Dead("DRUM_3")
THEN
	RESPONSE #100
		ChangeAIScript("",CLASS)
END

IF
	//IsHeartOfFuryModeOn()
	NightmareModeOn()
	Dead("DRUM_3")
	!Dead("DRUM_2")
THEN
	RESPONSE #100
		ChangeAIScript("30DrumP2",CLASS)
END

IF
	//IsHeartOfFuryModeOn()
	NightmareModeOn()
	Dead("DRUM_3")
	!Dead("DRUM_1")
THEN
	RESPONSE #100
		ChangeAIScript("30DrumP1",CLASS)
END

IF
	Global("I_PLAY_DRUMS","LOCALS",0)
	OR(4)
		!Global("TEAM_15","MYAREA",0)
		Global("DRUM_3_STATE","MYAREA",1)
		See(NearestEnemyOf(Myself))
		AttackedBy([PC],DEFAULT)
THEN
	RESPONSE #100
		SetGlobal("DRUM_3_STATE","MYAREA",2)
		SetGlobal("I_PLAY_DRUMS","LOCALS",1)
		//SetCreatureAreaFlag(Myself,IGNORE_RETURN_TO_START_POS,TRUE)
END

IF
	!Global("I_PLAY_DRUMS","LOCALS",0)
	ActionListEmpty()
	Global("PLAYING_DRUM","LOCALS",0)
	!Dead("DRUM_3")
	Range("DRUM_3",5)
THEN
	RESPONSE #100
		SetGlobal("PLAYING_DRUM","LOCALS",1)
		StartCutScene("30cDrum3")
	RESPONSE #10
		SetGlobal("PLAYING_DRUM","LOCALS",1)
		DisplayStringHead(Myself,@472)
		StartCutScene("30cDrum3")
END

IF
	!Global("PLAYING_DRUM","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("PLAYING_DRUM","LOCALS",0)
		Continue()
END

IF
	!Global("I_PLAY_DRUMS","LOCALS",0)
	OR(2)
		!Global("PLAYING_DRUM","LOCALS",0)
		Range("DRUM_3",5)
THEN
	RESPONSE #100
		NoAction()
END

IF
	!Global("I_PLAY_DRUMS","LOCALS",0)
	!Dead("DRUM_3")
THEN
	RESPONSE #100
		MoveToObject("DRUM_3")
END

