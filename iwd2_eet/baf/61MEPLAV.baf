IF
	Global("INITIALIZED_61MEPLAV","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("BreathCount","LOCALS",8)
		SetGlobal("INITIALIZED_61MEPLAV","LOCALS",1)
		Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	Allegiance(Myself,ENEMY)
	GlobalGT("BreathCount","LOCALS",0)
	!GlobalTimerNotExpired("BreathTimer","LOCALS")
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		IncrementGlobal("BreathCount","LOCALS",-1)
		FaceObject(LastSeenBy(Myself))
		SetSequence(SEQ_ATTACK)
		SmallWait(7)
		ReallyForceSpellRES("IWWI089",LastSeenBy(Myself))
		SetGlobalTimer("BreathTimer","LOCALS",21)
END

IF
	Allegiance(Myself,ENEMY)
	Global("Gate","LOCALS",0)
	Global("61LavaMephitDied","GLOBAL",1)
	RandomNum(10,1)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CreateVisualEffectObject("CONJUH",Myself)
		CreateCreature("61mepLa1",[-1.-1],0)
		SetGlobal("61LavaMephitDied","GLOBAL",0)
		SetGlobal("Gate","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	Global("Fireworks","LOCALS",0)
	RandomNum(5,1)
	See(NearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_BLIND)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		FaceObject(LastSeenBy(Myself))
		SetSequence(SEQ_ATTACK)
		SmallWait(7)
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_BLINDNESS)
		SetGlobal("Fireworks","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	See(NearestEnemyOf(Myself))
	!TargetUnreachable(LastSeenBy(Myself))
THEN
	RESPONSE #100
		//EquipWeapon()
		AttackOneRound(LastSeenBy(Myself))
END

IF
	!Range([PC],10)
THEN
	RESPONSE #100
		RandomWalk()
END

